/CLEAR

! reverse video
/RGB,INDEX,100,100,100, 0   
/RGB,INDEX, 80, 80, 80,13   
/RGB,INDEX, 60, 60, 60,14   
/RGB,INDEX, 0, 0, 0,15  
/REPLOT
! set view angle
/AUTO,1
/VIEW,1,-1,-1,0
/DIST,1,0.26,0
/REP,FAST

! input mesh
/INPUT,'Assembly8','in'

/PREP7
! set TC4 material properties
MP,EX,1, 10.9e10

! set element type 2,3&4 to 1 (solid185)
ESEL,S,TYPE,,2
ESEL,A,TYPE,,3
ESEL,A,TYPE,,4
EMODIF,ALL,TYPE,1
! delete element type 2,3&4
ETDEL,2 
ETDEL,3
ETDEL,4
ALLS

! set element type 6&7 to 5 (shell181)
ESEL,S,TYPE,,6
ESEL,A,TYPE,,7
EMODIF,ALL,TYPE,5
! delete element type 6&7
ETDEL,6
ETDEL,7
ALLS

! ! remove water
! CMSEL,S,WATER/WATER 
! ESLN,S,1,ALL
! CMSEL,S,DIA
! ESLN,U,1,ALL 
! CMSEL,S,TANK2
! ESLN,U,1,ALL 
! EDELE,ALL
! NDELE,ALL
! ALLS

! remove stand
CMSEL,S,STAND/STAND
ESLN,S,1,ALL
EDELE,ALL
NDELE,ALL
ALLS

! remove damper
CMSEL,S,DAMPER/DAMPER
ESLN,S,1,ALL
EDELE,ALL
NDELE,ALL
ALLS

! ! remove tank1
! CMSEL,S,TANK1/TANK1_1
! ESLN,S,1,ALL
! EDELE,ALL
! NDELE,ALL
! ALLS

! create shell sections
SECTYPE,1,SHELL
SECDATA,0.0012
SECTYPE,2,SHELL
SECDATA,0.0005
! allocate shell sections
CMSEL,S,TANK1/TANK1_1
ESLN,S,1,ALL 
EMODIF,ALL,SECNUM,1 
CMSEL,S,TANK2
ESLN,S,1,ALL 
EMODIF,ALL,SECNUM,1 
CMSEL,S,DIA
ESLN,S,1,ALL 
ESEL,U,TYPE,,22
EMODIF,ALL,SECNUM,2
ALLS

! create beam elements and sections
ET,2,188
SECTYPE,3,BEAM,RECT
SECDATA,0.0012,0.0012
! select beam nodes
ESEL,S,TYPE,,22
EMODIF,ALL,TYPE,2
EMODIF,ALL,SECNUM,3
! delete element type 22
ETDEL,22
ALLS

! create contact and target elements
ET,17,TARGE170
KEYOPT,17,5,5
ET,18,CONTA177
KEYOPT,18,2,2
KEYOPT,18,12,5
KEYOPT,18,5,0
KEYOPT,18,11,1
! create real constants
R,5 ! tank1 and ring contact
R,6 ! tank2 and ring contact
R,7 ! dia and ring contact

! tank1 and ring contact
CMSEL,S,RING_TANK1_SURF  
TYPE,17 ! element type 17 target 170
REAL,5 ! real constant 5
ESURF ! create target element
ALLS
CMSEL,S,TANK1/TANK1_1 
NSEL,R,LOC,X,0.3385e-1  
TYPE,18 ! element type 17 contact 177
REAL,5 ! real constant 5
ESURF ! create contact element
ALLS
SHSD,5,surface 

! tank2 and ring contact
CMSEL,S,RING_TANK2_SURF  
TYPE,17 ! element type 17 target 170
REAL,6 ! real constant 6
ESURF ! create target element
ALLS
CMSEL,S,TANK2
NSEL,R,LOC,X,0.67782E-001  
TYPE,18 ! element type 18 contact 177
REAL,6 ! real constant 6
ESURF ! create contact element
ALLS
SHSD,6,surface 

! dia and ring contact
CMSEL,S,RING_DIA_SURF  
TYPE,17 ! element type 17 target 170
REAL,7 ! real constant 7
ESURF ! create target element
ALLS
CMSEL,S,DIA
NSEL,R,LOC,X,0.45350E-001  
TYPE,18 ! element type 18 contact 177
REAL,7 ! real constant 7
ESURF ! create contact element
ALLS
SHSD,7,surface 

! ! ring and stand contact
! CMSEL,S,STAND_RING_SURF
! ESLN,S,,ALL
! CMSEL,S,RING_STAND_SURF
! CMSEL,R,RING_BOLT_SURF
! CEINTF,,UX,UY,UZ
! ALLS

! ! fixed support on stand
! NSEL,S,LOC,X,0.37275
! D,ALL,UX
! D,ALL,UY
! D,ALL,UZ
! ALLS

! fixed support on ring
CMSEL,S,RING_BOLT_SURF
D,ALL,UX
D,ALL,UY
D,ALL,UZ
ALLS

! gravity
ACEL,9.8,,,

! ! modal analysis
! /SOLU
! ANTYPE,2
! MODOPT,LANB,300,10,2100
! EQSLV,SPAR
! MXPAND,300,10,2100,1 
! OUTRES,ALL,,,,
! SOLVE 
! FINISH

! ! list all modal frequency
! /POST1
! SET,LIST

! create water element and material
ET,3,30,0,0
! incompressible
KEYOPT,3,6,1
MP,DENS,2,998.2
MP,SONC,2,1482.1
MPDE,EX,2   
MPDE,PRXY,2 
MPDE,NUXY,2 
R,8,20e-6,  
! select water elements
CMSEL,S,WATER/WATER 
ESLN,S,1,ALL
CMSEL,S,DIA
ESLN,U,1,ALL 
CMSEL,S,TANK2
ESLN,U,1,ALL 
ESEL,U,TYPE,,2
! allocate water
EMODIF,ALL,TYPE,3
EMODIF,ALL,MAT,2
EMODIF,ALL,REAL,8
ALLS

! water and ring contact
CMSEL,S,RING_WATER_SURF
CMSEL,A,RING_DIA_SURF
ESLN,S,,ALL
ESEL,U,TYPE,,17
ESEL,U,TYPE,,18
CMSEL,S,WATER/WATER_RING_SURF
CMSEL,U,DIA
CMSEL,U,TANK2
CEINTF,,UX,UY,UZ
ALLS

! fluid-solid interface
CMSEL,S,DIA
CMSEL,A,TANK2
CMSEL,A,WATER/WATER_RING_SURF
ESLN,S,0,ALL
CMSEL,S,DIA
ESLN,U,1,ALL 
CMSEL,S,TANK2
ESLN,U,1,ALL 
ESEL,U,TYPE,,2
ESEL,U,TYPE,,23
ESEL,U,TYPE,,24
ESEL,U,TYPE,,25
CMSEL,S,DIA
CMSEL,A,TANK2
CMSEL,A,WATER/WATER_RING_SURF
SF,ALL,FSI
ALLS

! ! modal analysis
! /SOLU
! ANTYPE,2
! MODOPT,LANB,10
! EQSLV,SPAR  
! MXPAND,10,,,1 
! OUTRES,ALL,,,,
! SOLVE 
! FINISH

! ! list all modal frequency
! /POST1
! SET,LIST


! modal analysis
/solu
ANTYPE,MODAL
MODOPT,UNSYM,300,10,2100,,,BOTH    ! request BOTH left and right eigensolutions
MXPAND,300,10,2100,yes,,,,
OUTRES,ALL,ALL,,,
SOLVE
FINISH

! list all modal frequency
/POST1
SET,LIST
! CMSEL,S,TANK1/TANK1_1
! ESLN,U,0,ALL
! ESEL,U,TYPE,,23
! ESEL,U,TYPE,,24
! ESEL,U,TYPE,,25
! ESEL,U,TYPE,,3

! random analysis
/SOLU
ANTYPE,SPECTRUM
SPOPT,PSD,194,yes
! define base excitation
CMSEL,S,RING_BOLT_SURF
D,ALL,UX,1.0
ALLS
! define damp
DMPRAT,0.02
! define excitation type
PSDUNIT,1,ACCG,9.81
! define excitation curve
PSDFRQ,1,,20,80,350,2000,
PSDVAL,1,0.0025,0.01,0.01,0.0017, 
! plot curve
! PSDGRAPH,1,,
! calculate participation factor
PFACT,1,BASE
! calculate dispalcement velocity accelration
PSDRES,DISP,ABS
PSDRES,VELO,ABS
PSDRES,ACEL,ABS
SOLVE
FINISH

! combine modes
/SOLU
ANTYPE,SPECTRUM,,,,,,
PSDCOM,0.0,138,TOTAL
SOLVE,
FINISH

! plot contour
/POST1
! CMSEL,S,TANK1/TANK1_1
! ESLN,U,0,ALL
ESEL,U,TYPE,,23
ESEL,U,TYPE,,24
ESEL,U,TYPE,,25
ESEL,U,TYPE,,3
! plot stress
SET,3,1,3,,,,,
PLNSOL,S,EQV,,
! plot acceleration
SET,5,1,3,,,,,
PLNSOL,U,X,,

! plot psd curve
/POST26
STORE,PSD,10,,
! define variable
NSOL,2,16709,U,X,UX16709,
RPSD,3,2,,3,1,PSDA16709,
NSOL,4,16917,U,X,UX16917,
RPSD,5,4,,3,1,PSDA16917,
NSOL,6,40605,U,X,UX40605,
RPSD,7,6,,3,1,PSDA40605,
NSOL,8,2532,U,X,UX2532,
RPSD,9,8,,3,1,PSDA2532,
! adjust plot
PLTIME,20,2000
/AXLAB,X,FREQ
/AXLAB,Y,ACCG
/GROPT,LOGX,ON
/GROPT,LOGY,ON
/GROPT,AXDV,OFF
! plot
PLVAR,3,5,7,9,,,,,,

! AirChamber Endpoint: 2532
! Diaphragm Endpoint: 16709
! WaterChamber Endpoint: 16917
! Lug:40605

! start from 10:23